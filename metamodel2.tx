RosAmqpConnectorsMetamodel:
    amqpBroker=AMQPBroker
    rosSystem*=ROSSystem
    rosSubConnectors*=ROSSubConnector
    rosPubConnectors*=ROSPubConnector
;

AMQPBroker:
   	'AMQPBroker' '=> {'
		'ip =' ip=STRING ';'
		'port =' port=STRING ';'
		'username =' username=STRING ';'
		'password =' password=STRING ';'

        'section #topics'
            topics*=AMQPTopic
        'endsection'
	'}'
;

AMQPTopic:
	'AMQPTopic' name=ID '=> {'
		'exchange =' exchange=STRING ';'
		'topic =' topic=STRING ';'
		'namespace =' namespace=STRING ';'
		'vhost =' vhost=STRING ';'
	'}'
;

ROSPubConnector:
    'Connector' name=ID rosPub=[ROSPublisher] '->' amqpTopic=[AMQPTopic] ';'
;

ROSSubConnector:
    'Connector' name=ID rosSub=[ROSSubscriber] '<-' amqpTopic=[AMQPTopic] ';'
;

ROSSystem:
   	'ROSSystem' '=> {'
        ('section #publishers'
            publishers*=ROSPublisher
        'endsection')?

        ('section #subscribers'
            subscribers*=ROSSubscriber
        'endsection')?

        ('section #topics'
            topics*=ROSTopic
        'endsection')?
    '}'
;

ROSPublisher:
    'ROSPublisher' name=ID '=> {'
        'topic =' topic=[ROSTopic] ';'
        ('nodeName =' nodeName=STRING ';')?
        ('queueSize =' queueSize=INT ';')?
        ('latch =' latch=BOOL ';')?
    '}'
;

ROSSubscriber:
    'ROSSubscriber' name=ID '=> {'
        'topic =' topic=[ROSTopic] ';'
        ('nodeName =' nodeName=STRING ';')?
    '}'
;

ROSTopic:
    'ROSTopic' name=ID '=> {'
        'name =' uri=STRING ';'
		'namespace =' namespace=STRING ';'
        'msgType =' msgType=STRING ';'
    '}'
;

Comment:
  /\/\/.*$/
;
